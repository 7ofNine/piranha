PROJECT(piranha)
SET(CMAKE_MODULE_PATH ${CMAKE_CURRENT_SOURCE_DIR}/cmake_modules)
INCLUDE(${CMAKE_ROOT}/Modules/CMakeDependentOption.cmake)
INCLUDE(${CMAKE_ROOT}/Modules/FindPythonLibs.cmake)
INCLUDE(FindGMP)
INCLUDE(FindGSL)
INCLUDE(PiranhaCompilerLinkerSettings)
INCLUDE(PiranhaFortranSetup)
INCLUDE(PiranhaPythonSetup)
INCLUDE(CheckIncludeFiles)
INCLUDE(CPack)
INCLUDE(CheckTypeSize)
INCLUDE(CheckCXXCompilerFlag)
ENABLE_LANGUAGE(C CXX)
INCLUDE_DIRECTORIES("external_headers")

# Build options
OPTION(BUILD_MAIN "Build 'main.cpp'." OFF)
CMAKE_DEPENDENT_OPTION(BUILD_PYRANHA_FS "Enable Fourier series in Pyranha." ON "BUILD_PYRANHA" OFF)
CMAKE_DEPENDENT_OPTION(BUILD_PYRANHA_MPFS "Enable multiple precision Fourier series in Pyranha." OFF "BUILD_PYRANHA" OFF)
CMAKE_DEPENDENT_OPTION(BUILD_PYRANHA_CFS "Enable compacted Fourier series in Pyranha." OFF "BUILD_PYRANHA" OFF)
CMAKE_DEPENDENT_OPTION(BUILD_PYRANHA_SP "Build sp series in Pyranha." OFF "BUILD_PYRANHA" OFF)
OPTION(ENABLE_ASSERTS "Enable asserts in code, for debugging." OFF)
OPTION(ENABLE_GSL_INTEGRATION "Enable integration with the GNU Scientific Library." OFF)
OPTION(BUILD_FORTRAN "Enable support for Fortran routines \(at the moment used by TASS\)" OFF)
OPTION(BUILD_PYRANHA "Build Python bindings." OFF)
CMAKE_DEPENDENT_OPTION(BUILD_PYRANHA_GUI "Build Qt4 gui for pyranha." OFF "BUILD_PYRANHA" OFF)
OPTION(BUILD_TBB_MULTITHREADING "Use Intel's Thread Building Blocks for multi-threaded parallelism. (CURRENTLY INEFFECTIVE)" OFF)
OPTION(BUILD_TESTS "Build performance and correctness tests." OFF)
OPTION(ENABLE_HOARD "Use the Hoard memory allocator - EXPERIMENTAL." OFF)
OPTION(ENABLE_SSE2 "Enable series type using SSE2 instructions (requires a SSE2-enabled CPU) - EXPERIMENTAL." OFF)
OPTION(ENABLE_DISPLAY_PROGRESS "Enable progress display during long operations." ON)

SET(MANDATORY_LIBRARIES "")
SET(PIRANHA_DEFINITIONS "")

CHECK_TYPE_SIZE("void *" POINTER_SIZE)
MESSAGE(STATUS "Pointer size is " ${POINTER_SIZE})

IF(NOT ENABLE_DISPLAY_PROGRESS)
  SET(PIRANHA_DEFINITIONS ${PIRANHA_DEFINITIONS} -D_PIRANHA_DISPLAY_PROGRESS)
ENDIF(NOT ENABLE_DISPLAY_PROGRESS)

IF(BUILD_TBB_MULTITHREADING)
  IF(WIN32)
# This is irrelevant at the moment, since TBB does not compile yet in MinGW.
    SET(PIRANHA_DEFINITIONS ${PIRANHA_DEFINITIONS} -DUSE_WINTHREAD)
  ELSE(WIN32)
    SET(PIRANHA_DEFINITIONS ${PIRANHA_DEFINITIONS} -DUSE_PTHREAD)
    SET(MANDATORY_LIBRARIES ${MANDATORY_LIBRARIES} pthread dl)
  ENDIF(WIN32)
  SET(PIRANHA_DEFINITIONS ${PIRANHA_DEFINITIONS} -D_PIRANHA_TBB)
  EXECUTE_PROCESS(COMMAND sh version_info_linux.sh OUTPUT_FILE src/tbb/version_string.tmp)
ENDIF(BUILD_TBB_MULTITHREADING)

IF(${POINTER_SIZE} EQUAL 8)
  MESSAGE(STATUS "64bit platform detected")
  SET(PIRANHA_DEFINITIONS ${PIRANHA_DEFINITIONS} -D_PIRANHA_64BIT)
ENDIF(${POINTER_SIZE} EQUAL 8)

PIRANHA_COMPILER_LINKER_SETTINGS()
IF(BUILD_FORTRAN)
  PIRANHA_FORTRAN_SETUP()
ENDIF(BUILD_FORTRAN)
IF(BUILD_PYRANHA)
  PIRANHA_PYTHON_SETUP()
ENDIF(BUILD_PYRANHA)

# Set linking flags.
SET(MANDATORY_LIBRARIES ${MANDATORY_LIBRARIES} ${LINK_FLAGS})

# Platform switches
IF(WIN32)
  SET(PIRANHA_DEFINITIONS ${PIRANHA_DEFINITIONS} -D_PIRANHA_WIN32)
# TODO: generalize this using variables, e.g. %SystemRoot and such?
  SET(LIB_INSTALL_PATH "c:/windows/system32")
ELSE(WIN32)
  SET(LIB_INSTALL_PATH "lib")
ENDIF(WIN32)

# Find GMP.
FIND_PACKAGE(GMP REQUIRED)
SET(MANDATORY_LIBRARIES ${MANDATORY_LIBRARIES} ${GMP_LIBRARIES} ${GMPXX_LIBRARIES})

# Find GSL.
IF(ENABLE_GSL_INTEGRATION)
  FIND_PACKAGE(GSL)
  IF(NOT GSL_FOUND)
    MESSAGE(FATAL_ERROR "GSL integration was requested, but GSL libraries or include dir could not be located.")
  ENDIF(NOT GSL_FOUND)
  MESSAGE(STATUS "Enabling GSL integration.")
  SET(MANDATORY_LIBRARIES ${MANDATORY_LIBRARIES} ${GSL_LIBRARIES} ${GSLCBLAS_LIBRARIES})
  SET(PIRANHA_DEFINITIONS ${PIRANHA_DEFINITIONS} -DHAVE_INLINE -D_PIRANHA_GSL)
ENDIF(ENABLE_GSL_INTEGRATION)

# SSE2
IF(ENABLE_SSE2)
  SET(PIRANHA_DEFINITIONS ${PIRANHA_DEFINITIONS} -D_PIRANHA_SSE2)
  CHECK_INCLUDE_FILES(emmintrin.h HAVE_EMMINTRIN_H)
  IF(NOT HAVE_EMMINTRIN_H)
    MESSAGE(FATAL_ERROR "Missing SSE2 intrinsics header.")
  ENDIF(NOT HAVE_EMMINTRIN_H)
  CHECK_INCLUDE_FILES(mm_malloc.h HAVE_MM_MALLOC_H)
  IF(NOT HAVE_MM_MALLOC_H)
    SET(PIRANHA_DEFINITIONS ${PIRANHA_DEFINITIONS} -D_PIRANHA_PRIVATE_MM_MALLOC_H)
  ENDIF(NOT HAVE_MM_MALLOC_H)
ENDIF(ENABLE_SSE2)

# Common stuff
SET(CMAKE_BUILD_TYPE "Release")
# Find out motion theories files to be installed
SET(THEORIES_INSTALL_PATH "${CMAKE_INSTALL_PREFIX}/share/piranha/theories")
SET(PIRANHA_DEFINITIONS ${PIRANHA_DEFINITIONS} -D_GNU_SOURCE -D_PIRANHA_THEORIES_DIR=\\"${THEORIES_INSTALL_PATH}\\")
IF(ENABLE_ASSERTS)
  SET(PIRANHA_DEFINITIONS ${PIRANHA_DEFINITIONS} -D_ENABLE_ASSERTS)
ENDIF(ENABLE_ASSERTS)
IF(BUILD_TBB_MULTITHREADING OR ENABLE_HOARD)
  SET(PIRANHA_DEFINITIONS ${PIRANHA_DEFINITIONS} -D_REENTRANT)
ENDIF(BUILD_TBB_MULTITHREADING OR ENABLE_HOARD)

# Hoard is not supported ATM on Win32.
IF(ENABLE_HOARD AND WIN32)
  MESSAGE(FATAL_ERROR "The Hoard memory allocator is not supported, at the moment, under MinGW.")
ENDIF(ENABLE_HOARD AND WIN32)

# TBB is not supported ATM on Win32.
IF(BUILD_TBB_MULTITHREADING AND WIN32)
  MESSAGE(FATAL_ERROR "Intel TBB cannot be compiled in MinGW at the moment.")
ENDIF(BUILD_TBB_MULTITHREADING AND WIN32)

ADD_DEFINITIONS(${PIRANHA_DEFINITIONS})

# Add library directory
ADD_SUBDIRECTORY(src)

# Build a main.cpp file included in the source dir
IF(BUILD_MAIN)
  ADD_EXECUTABLE(main main.cpp)
  TARGET_LINK_LIBRARIES(main ${MANDATORY_LIBRARIES} piranha_static)
ENDIF(BUILD_MAIN)

IF(BUILD_TESTS)
  ADD_SUBDIRECTORY(tests)
ENDIF(BUILD_TESTS)

# pyranha: piranha's python bindings
IF(BUILD_PYRANHA)
# Mandatory linking.
  SET(MANDATORY_LIBRARIES ${MANDATORY_LIBRARIES} piranha)
  ADD_SUBDIRECTORY(pyranha)
ENDIF(BUILD_PYRANHA)

# Install phase: find and install theories of motion
FILE(GLOB_RECURSE THEORIES theories_of_motion/*.csv theories_of_motion/*.phl theories_of_motion/*.dat)
INSTALL(FILES ${THEORIES} DESTINATION ${THEORIES_INSTALL_PATH})
