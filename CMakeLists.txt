PROJECT(piranha)
SET(CMAKE_MODULE_PATH ${CMAKE_CURRENT_SOURCE_DIR}/cmake_modules)
INCLUDE(${CMAKE_ROOT}/Modules/CMakeDependentOption.cmake)
INCLUDE(${CMAKE_ROOT}/Modules/FindPythonLibs.cmake)
INCLUDE(FindGMP)
INCLUDE(PiranhaCompilerLinkerSettings)
INCLUDE(PiranhaFortranSetup)
INCLUDE(PiranhaPythonSetup)
INCLUDE(CPack)
ENABLE_LANGUAGE(C CXX)
INCLUDE_DIRECTORIES("external_headers")

# Build options
OPTION(BUILD_MAIN "Build 'main.cpp'." OFF)
OPTION(BUILD_NP "Build numerical series with array lists." ON)
OPTION(BUILD_LNP "Build numerical series with trigonometric lists." OFF)
OPTION(BUILD_SP "Build sp series." OFF)
CMAKE_DEPENDENT_OPTION(BUILD_KEPLERIAN_PROCESSOR "Build keplerian processor." OFF "BUILD_SP" OFF)
CMAKE_DEPENDENT_OPTION(BUILD_TASS "Build TASS17 functions." ON "BUILD_LNP" OFF)
OPTION(BUILD_FORTRAN "Enable support for Fortran routines \(at the moment used by TASS\)" OFF)
OPTION(BUILD_PYRANHA "Build Python bindings." OFF)
CMAKE_DEPENDENT_OPTION(BUILD_PYRANHA_GUI "Build Qt4 gui for pyranha." OFF "BUILD_PYRANHA" OFF)
OPTION(ENABLE_MULTITHREADING "Use multiple threads for calculations. (CURRENTLY INEFFECTIVE)" OFF)

SET(MANDATORY_LIBRARIES "")

PIRANHA_COMPILER_LINKER_SETTINGS()
IF(BUILD_FORTRAN)
  PIRANHA_FORTRAN_SETUP()
ENDIF(BUILD_FORTRAN)
IF(BUILD_PYRANHA)
  PIRANHA_PYTHON_SETUP()
ENDIF(BUILD_PYRANHA)

# Set linking flags.
SET(MANDATORY_LIBRARIES ${MANDATORY_LIBRARIES} ${LINK_FLAGS})

# Platform switches
IF(WIN32)
  ADD_DEFINITIONS(-D_PIRANHA_WIN32)
# TODO: generalize this using variables, e.g. %SystemRoot and such?
  SET(LIB_INSTALL_PATH "c:/windows/system32")
ELSE(WIN32)
  SET(LIB_INSTALL_PATH "lib")
ENDIF(WIN32)

# Find GMP if required.
IF(BUILD_SP)
  FIND_PACKAGE(GMP REQUIRED)
  SET(MANDATORY_LIBRARIES ${MANDATORY_LIBRARIES} ${GMP_LIBRARIES} ${GMPXX_LIBRARIES})
ENDIF(BUILD_SP)

# TASS
IF(BUILD_TASS)
  ADD_DEFINITIONS(-D_PIRANHA_TASS)
ENDIF(BUILD_TASS)

# Common stuff
SET(CMAKE_BUILD_TYPE "Release")
# Find out motion theories files to be installed
SET(THEORIES_INSTALL_PATH "${CMAKE_INSTALL_PREFIX}/share/piranha/theories")
SET(PIRANHA_DEFINITIONS -D_ENABLE_ASSERTS -D_GNU_SOURCE -D_PIRANHA_THEORIES_DIR=\\"${THEORIES_INSTALL_PATH}\\")
IF(ENABLE_MULTITHREADING)
  SET(PIRANHA_DEFINITIONS ${PIRANHA_DEFINITIONS} -D_REENTRANT)
ENDIF(ENABLE_MULTITHREADING)
ADD_DEFINITIONS(${PIRANHA_DEFINITIONS})

# Build a main.cpp file included in the source dir
IF(BUILD_MAIN)
  ADD_EXECUTABLE(main main.cpp)
  TARGET_LINK_LIBRARIES(main ${MANDATORY_LIBRARIES} piranha_static)
ENDIF(BUILD_MAIN)

# Add library directory
ADD_SUBDIRECTORY(src)

# pyranha: piranha's python bindings
IF(BUILD_PYRANHA)
# Mandatory linking.
  SET(MANDATORY_LIBRARIES ${MANDATORY_LIBRARIES} piranha)
  ADD_SUBDIRECTORY(pyranha)
ENDIF(BUILD_PYRANHA)

# Install phase: find and install theories of motion
FILE(GLOB_RECURSE THEORIES theories_of_motion/*.csv theories_of_motion/*.phl theories_of_motion/*.dat)
INSTALL(FILES ${THEORIES} DESTINATION ${THEORIES_INSTALL_PATH})
